 Using←{
⍝ ⍵: [packages] (empty: all)
⍝ ⍺: dotnetversion (default: running)
     ⍺←''
     dnv←{0∊⍴⍵:⊃GetDotNetRunningVersion ⋄ GetDotNetVersion ⍵}⍺
     0=dnv:'.NET unavailable'⎕SIGNAL 11
     pref←dnv⊃('netstandard' 'net6.0')

     Prefix←{⍵/⍨∧\'.'≠⍵}
     pkg←{0∊⍴⍵:⊣/dependencies ⋄ ⊆⍵}⍵
     pi←(⊣/dependencies)⍳pkg
     nf←∨/pi>≢dependencies:'Packages not found: ',⍕nf/pkg
     ver←⊂¨(⊢/dependencies)[(⊣/dependencies)⍳pkg]
     (paths versions dotnet)←Versions pkg
     ∨/~ver∊¨versions:   ⍝ Version not found

     I←{(⊂⍵)⌷⍺}
     avail←(⊃,/)¨dotnet I¨versions⍳¨ver
     fwk←avail/¨⍨pref∘(∨/⍷)¨¨avail   ⍝ PrefixOf
     no←1>≢¨fwk
     ∨/no:'Packages: (',(⍕no/⍵),') not available for .NET version ',(⍕pref)
     fwk←{⊃(⊂⍒⍵)⌷⍵}¨fwk
     dll←,∘'.dll'¨Prefix¨pkg
     ver←⊃¨ver ⋄ lib←⊂'lib'
     JoinPath←⊃(⊣,'/',⊢)/
     using←JoinPath¨↓⍉↑paths ver lib fwk dll
     ∨/no←~⎕NEXISTS using:
     pkg,¨','∘,¨using
 }
